// UL calibration :: RPMs: 190  (02 Septiembre 2026)

    // En proceso de calibracion  

// Calibration    <<<<<UL>>>>>>

//Use this code to calibrate

// Almost last code or test for the Encoders
// Works (4:50 am)
// FINAL gives velocit to the motor

#include <Arduino.h>

const uint8_t encUL_B_Pin = 1;
const uint8_t encUL_A_Pin = 0;
const uint8_t motorPWM    = 8;
const uint8_t motorIN1    = 33;
const uint8_t motorIN2    = 34;

// Encoder Filter
#define FILTER_SIZE 12

volatile unsigned long period_buf[FILTER_SIZE] = {0};
volatile uint8_t       period_idx  = 0;
volatile unsigned long last_pulse_us = 0;
volatile bool          got_pulse   = false;



//  PID 
const float PPR = 190.0f;
const float Ts  = 0.05f;   // 50ms — más estable que 10ms

float Kp = 0.5f;//2.2f;
float Ki = 0.6f;//0.8f;
float Kd = 0.0f;//0.0022f;

float setpoint   = 45.0f;

// Feedforward UL: recta PWM = offset + pendiente * rpm.
// Medido EN EL AIRE:  30 rpm ~ 42 PWM, 45 rpm ~ 58.7 PWM  (pendiente ~1.1)
// Medido EN EL SUELO: 45 rpm ~ 80.5 PWM, 63.5 rpm (PWM fijo 100)  (pendiente ~1.05, offset ~33)
// Se deja un poco por debajo de lo medido (el integrador completa lo que falte).
// Confirmado en el suelo: 30 rpm ~ 65 PWM, 45 rpm ~ 82 PWM.
const float FF_OFFSET = 32.0f;  // PWM
const float FF_SLOPE  = 1.05f;  // PWM por rpm  -> 30 rpm: 63.5, 45 rpm: 79
// Rampa del setpoint efectivo para no arrancar con error grande
const float SP_RAMP = 90.0f;    // rpm/s
float sp_ramped = 0.0f;

float integral   = 0.0f;
float last_error = 0.0f;

// Filtro EMA sobre el RPM medido (suaviza el ruido de cuantizacion del encoder)
const float RPM_ALPHA = 0.2f; // 0=sin cambio, 1=sin filtro
float rpm_filt = 0.0f;

unsigned long last_time = 0;

//  Helpers ISR 
void pushPeriod(unsigned long p)
{
    period_buf[period_idx] = p;
    period_idx = (period_idx + 1) % FILTER_SIZE;
    got_pulse  = true;
}

//  ISR 
void isrA()
{
    unsigned long now = micros();
    unsigned long p   = now - last_pulse_us;
    last_pulse_us     = now;
    if (p > 200UL) pushPeriod(p);  // Ignora rebotes < 200µs
}

void isrB()
{
    unsigned long now = micros();
    unsigned long p   = now - last_pulse_us;
    last_pulse_us     = now;
    if (p > 200UL) pushPeriod(p);  // Ignora rebotes < 200µs
}

//  Measurement RPM 
float measureRPM()
{
    noInterrupts();
    unsigned long buf[FILTER_SIZE];
    for (uint8_t i = 0; i < FILTER_SIZE; i++) buf[i] = period_buf[i];
    unsigned long last    = last_pulse_us;
    bool          has_got = got_pulse;
    interrupts();

    if (!has_got) return 0.0f;
    if (micros() - last > 200000UL) return 0.0f;

    // Average of valid periods
    unsigned long sum   = 0;
    uint8_t       count = 0;
    for (uint8_t i = 0; i < FILTER_SIZE; i++) {
        if (buf[i] > 0) { sum += buf[i]; count++; }
    }
    if (count == 0) return 0.0f;

    float avg_period = (float)(sum / count);
    return 60000000.0f / (avg_period * 4.0f * PPR);
}

// Motor 
void setMotor(float pwm)
{
    if (pwm >= 0.0f) {
        digitalWrite(motorIN1, HIGH);
        digitalWrite(motorIN2, LOW);
    } else {
        digitalWrite(motorIN1, LOW);
        digitalWrite(motorIN2, HIGH);
        pwm = -pwm;
    }
    analogWrite(motorPWM, (int)constrain(pwm, 0.0f, 255.0f));
}

//  Setup 
void setup()
{
    Serial.begin(115200);

    pinMode(encUL_A_Pin, INPUT_PULLUP);
    pinMode(encUL_B_Pin, INPUT_PULLUP);
    pinMode(motorIN1, OUTPUT);
    pinMode(motorIN2, OUTPUT);
    pinMode(motorPWM, OUTPUT);

    analogWriteResolution(8);
    analogWriteFrequency(motorPWM, 25000);

    attachInterrupt(digitalPinToInterrupt(encUL_A_Pin), isrA, CHANGE);
    attachInterrupt(digitalPinToInterrupt(encUL_B_Pin), isrB, CHANGE);

    last_time = millis();
}

//  Loop 
void loop()
{
    unsigned long now = millis();
    if (now - last_time >= (unsigned long)(Ts * 1000.0f))
    {
        last_time += (unsigned long)(Ts * 1000.0f);

        float rpm_raw = measureRPM();
        rpm_filt      += RPM_ALPHA * (rpm_raw - rpm_filt);
        float rpm     = rpm_filt;

        if (sp_ramped < setpoint)      sp_ramped = min(setpoint, sp_ramped + SP_RAMP * Ts);
        else if (sp_ramped > setpoint) sp_ramped = max(setpoint, sp_ramped - SP_RAMP * Ts);

        float error   = sp_ramped - rpm;
        float ff      = (sp_ramped > 0.0f) ? FF_OFFSET + FF_SLOPE * sp_ramped : 0.0f;

        float derivative       = (error - last_error) / Ts;
        float output_unclamped = ff + Kp * error + Ki * integral + Kd * derivative;

        // Solo integra si el output no esta saturado (anti-windup real)
        if (output_unclamped > 0.0f && output_unclamped < 255.0f) {
            integral += error * Ts;
            // Limite = rango completo de PWM (255) / Ki, para que Ki*integral pueda cubrir todo el output
            integral  = constrain(integral, -255.0f / Ki, 255.0f / Ki);
        }

        float output = constrain(output_unclamped, 0.0f, 255.0f);
        last_error    = error;

        setMotor(output);

        Serial.print("rpm_ul:");    Serial.print(rpm, 2);
        Serial.print(",Setpoint:"); Serial.print(setpoint);
        Serial.print(",output:");   Serial.println(output);
    }
}